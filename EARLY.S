	DFW EARLY_RCC_INIT, "EARLY-RCC-INIT"
	li x10, RCC_BASE
	lw x11, R32_RCC_APB2PCENR(x10)
	li x12, RCC_IOPA | RCC_UART1
	or x11, x11, x12
	sw x11, R32_RCC_APB2PCENR(x10)
	ret
	ENDW EARLY_RCC_INIT

	DFW EARLY_GPIO_INIT, "EARLY-GPIO-INIT"
	li x10, GPIOA_BASE
	lw x11, R32_GPIO_CFGHR(x10)
	li x12, ~((GPIO_MODE_MASK << 4) | (GPIO_MODE_MASK << 8) | (GPIO_MODE_MASK << 16))
	and x11, x11, x12
	li x12, ((GPIO_MODE_MUPPOUT_50M << 4) | (GPIO_MODE_FLOATIN << 8) | (GPIO_MODE_MUPPOUT_50M << 16))
	or x11, x11, x12
	sw x11, R32_GPIO_CFGHR(x10)
	ret
	ENDW EARLY_GPIO_INIT

	DFW EARLY_UART_INIT, "EARLY-UART-INIT"
	li x10, UART1_BASE
	li x11, UART_UE | UART_TE | UART_RE
	sw x11, R32_UART_CTLR1(x10)
	li x11, UART_RTSE
	sw x11, R32_UART_CTLR3(x10)
	li x11, ((0x34 << 0x4) | (0x1 << 0x0))
	sw x11, R32_UART_BRR(x10)
	ret
	ENDW EARLY_UART_INIT

	DFW EARLY_INIT, "EARLY-INIT"
	RPUSH x1
	NC W_EARLY_RCC_INIT
	NC W_EARLY_GPIO_INIT
	NC W_EARLY_UART_INIT
	RPOP x1
	ret
	ENDW EARLY_INIT

	DFW EARLY_EMITAVA, "EARLY-EMIT?"
	PPUSH x15
	li x14, UART1_BASE
	lw x13, R32_UART_STATR(x14)
	li x12, UART_TC
	and x11, x13, x12
	li x15, TRUE
	bnez x11, 1f
	li x15, FALSE
1:
	ret
	ENDW EARLY_EMITAVA

	DFW EARLY_EMIT, "EARLY-EMIT"
	RPUSH x1
1:	NC W_PAUSE; NC W_EARLY_EMITAVA;
	ZBRANCH 1b
	RPOP x1
	li x14, UART1_BASE
	sw x15, R32_UART_DATAR(x14)
	PPOP x15
	ret
	ENDW EARLY_EMIT

	DFW EARLY_KEYAVA, "EARLY-KEY?"
	PPUSH x15
	li x10, UART1_BASE
	lw x11, R32_UART_STATR(x10)
	li x12, UART_RXNE
	and x13, x11, x12
	li x15, TRUE
	bnez x13, 1f
	li x15, FALSE
1:
	ret
	ENDW EARLY_KEYAVA

	DFW EARLY_KEY, "EARLY_KEY"
	RPUSH x1
1:	NC W_PAUSE; NC W_EARLY_KEYAVA;
	ZBRANCH 1b
	RPOP x1
	li x10, UART1_BASE
	PPUSH x15
	lw x15, R32_UART_DATAR(x10)
	ret
	ENDW EARLY_KEY
