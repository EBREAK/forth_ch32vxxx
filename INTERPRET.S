	DFW TONLEN, ">NLEN"
	lbu x15, -6(x15)
	ret
	ENDW TONLEN

	DFW TONAME, ">NAME"
	lbu x14, -6(x15)
	addi x14, x14, 1 + 6
	andi x14, x14, -2
	sub x15, x15, x14
	ret
	ENDW TONAME

	DFW TOPREV, ">PREV"
	lhu x14, -4(x15)
	lhu x13, -2(x15)
	slli x13, x13, 16
	or x15, x14, x13
	andi x15, x15, ~(0x1) # REMOVE IMMEDIATE FLAG
	ret
	ENDW TOPREV

	.section .bss
	.P2ALIGN 2, 0
FORTH_LATEST:
	.4BYTE 0x0
	.section .text

	DFW FORTH_LATEST, "FORTH-LATEST"
	LITA FORTH_LATEST
	ret
	ENDW FORTH_LATEST

	DFW WORDLIST_WORDS, "WORDLIST-WORDS"
	RPUSH x1
	NC W_DUP; ZBRANCH 2f
1:
	NC W_DUP; NC W_TONAME; NC W_OVER; NC W_TONLEN; NC W_TYPE;
	NC W_SPACE; NC W_TOPREV; NC W_DUP; NC W_EQZ; ZBRANCH 1b
2:
	NC W_DROP;
	RPOP x1
	ret
	ENDW WORDLIST_WORDS

	DFW WORDLIST_SEARCH, "WORDLIST-SEARCH"
	RPUSH x1
	NC W_DUP; ZBRANCH 3f
1:
	NC W_DUP; NC W_TONAME; NC W_OVER; NC W_TONLEN;
	NC W_ROT; NC W_TOR; NC W_2OVER; NC W_COMPARE; NC W_FROMR; NC W_SWAP;
	ZBRANCH 3f
2:
	NC W_TOPREV; NC W_DUP; NC W_EQZ; ZBRANCH 1b
3:
	NC W_NIP; NC W_NIP;
	RPOP x1
	ret
	ENDW WORDLIST_SEARCH

	.section .bss
	.P2ALIGN 2, 0
FORTH_CONTEXT:
	.fill CONTEXT_MAX, 4, 0
	.section .text

	DFW CONTEXT, "CONTEXT"
	LITA FORTH_CONTEXT
	ret
	ENDW CONTEXT

	.section .bss
	.P2ALIGN 2, 0
USER_LATEST:
	.4BYTE 0x0
	.section .text

	DFW USER_LATEST, "USER-LATEST"
	LITA USER_LATEST
	ret
	ENDW USER_LATEST

	DFW WORDS, "WORDS"
	RPUSH x1
	NC W_CONTEXT;
1:
	NC W_DUP; lw x15, 0(x15); NC W_DUP; NC W_NEZ; ZBRANCH 2f
	NC W_CR; NC W_DUP; NC W_EXECUTE; lw x15, 0(x15); NC W_WORDLIST_WORDS;
2:
	NC W_DROP; addi x15, x15, 4;
	NC W_CONTEXT; NC W_CONTEXT_MAX; slli x15, x15, 2; NC W_PLUS;
	NC W_OVER; NC W_EQ; ZBRANCH 1b
	NC W_CR; NC W_DROP;
	RPOP x1
	ret
	ENDW WORDS

	DFW FIND, "FIND"
	RPUSH x1
	NC W_CONTEXT;
1:
	NC W_3DUP; lw x15, 0(x15); NC W_DUP; ZBRANCH 2f
	NC W_EXECUTE; lw x15, 0(x15); NC W_WORDLIST_SEARCH; NC W_DUP; ZBRANCH 3f
	NC W_2NIP; NC W_NIP; j 4f
2:
	NC W_2DROP;
3:
	NC W_DROP; addi x15, x15, 4;
	NC W_CONTEXT; NC W_CONTEXT_MAX; slli x15, x15, 2; NC W_PLUS;
	NC W_OVER; NC W_EQ; ZBRANCH 1b
	NC W_3DROP; NC W_0X0;
4:
	RPOP x1
	ret
	ENDW FIND

	DFW TIB, "TIB"
	PPUSH x15
	lw x15, USER_TIB(x4)
	ret
	ENDW TIB

	DFW TIB_NUM_USED, "TIB-NUM-USED"
	PPUSH x15
	lbu x15, USER_TIN(x4)
	ret
	ENDW TIB_NUM_USED

	DFW TIB_PUSH, "TIB-PUSH"
	lw x14, USER_TIB(x4)
	lbu x13, USER_TIN(x4)
	andi x13, x13, TIB_MASK
	add x14, x14, x13
	addi x13, x13, 1
	andi x13, x13, TIB_MASK
	sb x13, USER_TIN(x4)
	sb x15, 0(x14)
	PPOP x15
	ret
	ENDW TIB_PUSH

	DFW TIB_RESET, "TIB-RESET"
	sb zero, USER_TIN(x4)
	ret
	ENDW TIB_RESET

	DFW ISDELIM, "DELIM?"
	mv x14, x15
	li x15, TRUE
	li x13, 0x20
	beq x13, x14, 1f
	li x13, 0x0A
	beq x13, x14, 1f
	li x13, 0x0D
	beq x13, x14, 1f
	li x15, FALSE
1:
	ret
	ENDW ISDELIM

	DFW GET_WORD, "GET-WORD"
	RPUSH x1
	NC W_TIB_RESET;
1:
	NC W_KEY; NC W_DUP; NC W_ISDELIM; ZBRANCH 2f
	NC W_DROP; NC W_TIB_NUM_USED; ZBRANCH 1b; j 3f
2:
	NC W_TIB_PUSH; j 1b
3:
	RPOP x1
	ret
	ENDW GET_WORD

	DFW PSP_CHK, "PSP-CHK"
	mv x14, x9
	PPUSH x15
	li x15, FALSE
	lw x13, USER_PS0(x4)
	bgt x14, x13, 1f
	li x15, TRUE
1:
	ret
	ENDW PSP_CHK

	DFW PSP_RST, "PSP-RST"
	li x15, TOS_INIT
	lw x9, USER_PS0(x4)
	ret
	ENDW PSP_RST

	DFW STA_RST, "STA-RST"
	sw zero, USER_STA(x4)
	ret
	ENDW STA_RST

	DFW COMPON, "]"
	lw x14, USER_STA(x4)
	ori x13, x14, USER_STA_COMP
	sw x13, USER_STA(x4)
	ret
	ENDW COMPON

	IDFW COMPOFF, "["
	lw x14, USER_STA(x4)
	andi x13, x14, ~(USER_STA_COMP)
	sw x13, USER_STA(x4)
	ret
	ENDW COMPOFF

	DFW ISCOMPILE, "COMPILE?"
	PPUSH x15
	lw x14, USER_STA(x4)
	andi x14, x14, USER_STA_COMP
	li x15, TRUE
	bnez x14, 1f
	li x15, FALSE
1:
	ret
	ENDW ISCOMPILE

	DFW ISIMMEDIATE, "IMMEDIATE?"
	lbu x14, -4(x15)
	andi x13, x14, 1
	li x15, TRUE
	bnez x13, 1f
	li x15, FALSE
1:
	ret
	ENDW ISIMMEDIATE

	.section .rodata
MSG_NOT_FOUND:
	.ASCII " NOT FOUND\r\n"
	.SET LEN_MSG_NOT_FOUND, . - MSG_NOT_FOUND
	.section .text

	DFW INTERPRET, "INTERPRET"
	RPUSH x1
	NC W_GET_WORD;
	NC W_TIB; NC W_TIB_NUM_USED; NC W_FIND; NC W_DUP; ZBRANCH TRY_NUM
	NC W_DUP; NC W_ISIMMEDIATE; ZBRANCH 1f
	NC W_EXECUTE; j 8f
1:
	NC W_ISCOMPILE; ZBRANCH 2f
	# TODO
2:
	NC W_EXECUTE; j 8f
TRY_NUM:
	NC W_DROP;
	NC W_TIB; NC W_TIB_NUM_USED; NC W_ISNUMBER; ZBRANCH NOT_FOUND
	NC W_TIB; NC W_TIB_NUM_USED; NC W_TONUMBER;
	NC W_ISCOMPILE; ZBRANCH 3f
	# TODO
3:
	j 8f
NOT_FOUND:
	NC W_CR; NC W_TIB; NC W_TIB_NUM_USED; NC W_TYPE;
	LITA MSG_NOT_FOUND; LIT LEN_MSG_NOT_FOUND; NC W_TYPE;
	NC W_PSP_RST; NC W_STA_RST;
8:
	RPOP x1
	ret
	ENDW INTERPRET

	.section .rodata
MSG_PSP_UND:
	.ASCII "\r\nPARAM STACK UNDERFLOW\r\n"
	.SET LEN_MSG_PSP_UND, . - MSG_PSP_UND
	.section .text

	DFW REPL, "REPL"
1:
	NC W_INTERPRET
	NC W_PSP_CHK; xori x15, x15, -1; ZBRANCH 2f
	NC W_PSP_RST
	NC W_STA_RST
	LITA MSG_PSP_UND; LIT LEN_MSG_PSP_UND; NC W_TYPE;
2:
	j 1b
	ENDW REPL
